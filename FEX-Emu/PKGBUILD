# Maintainer: Jos Dehaes <jos.dehaes@gmail.com>
# Maintainer: Matthias Kurz <m.kurz@irregular.at>

pkgbase=FEX-Emu
pkgname=FEX-Emu
pkgver=2504
pkgrel=1
pkgdesc="Fast x86 emulation frontend"
arch=(aarch64)
url="https://github.com/FEX-Emu/FEX"
license=(MIT)
makedepends=(
  pkgconf
  cmake
  ninja
  git
  sed
  lld
  llvm
  clang
  libepoxy
  python
  qt6-declarative
  python-packaging
  python-setuptools
  xxhash
  libepoxy
  sdl2-compat
  libx11
  libxrandr
  wayland
)
depends=(
  passt
  squashfuse
  squashfs-tools
  erofs-utils
)
source=("git+https://github.com/FEX-Emu/FEX.git#tag=FEX-${pkgver}"
        "cmake.patch")
sha256sums=('SKIP'
            'SKIP')
install=FEX-Emu.install

prepare() {
  cd "${srcdir}/FEX"
  # Needed because the repo has submodules
  git submodule update --init --recursive
}

build() {
  cd ${srcdir}/FEX
  patch -p1 < ../../cmake.patch
  rm -rf build
  mkdir -p build || true
  cd build
  CC=clang CXX=clang++ cmake \
    -G Ninja \
    -DENABLE_OFFLINE_TELEMETRY=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_LINKER=lld \
    -DENABLE_LTO=ON \
    -DBUILD_TESTS=OFF \
    -DBUILD_FEX_LINUX_TESTS=OFF \
    -DENABLE_ASSERTIONS=ON \
    -DTUNE_CPU=apple-m1 \
    -DTUNE_ARCH=armv8-a \
    ..
  ninja
  if objdump -d Bin/FEXInterpreter | grep cnth; then
      echo "::error file=fex-emu::Found illegal instruction 'cnth'"
      exit 1
  fi
}

package() {
  cd ${srcdir}/FEX/build
  DESTDIR="${pkgdir}" ninja install
  mkdir -p "${pkgdir}"/usr/share/fex-emu/RootFS
  mkdir -p "${pkgdir}"/usr/share/fex-emu/overlays
}
