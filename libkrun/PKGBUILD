# Maintainer: hexchain <arch at hexchain.org>

pkgname=libkrun
pkgver=1.15.1
pkgrel=1
pkgdesc="A dynamic library providing Virtualization-based process isolation capabilities"
url='https://github.com/containers/libkrun'
arch=('x86_64' 'aarch64')
license=('Apache')
makedepends=('rustup' 'virglrenderer')
depends=('glibc' 'gcc-libs' 'libkrunfw' 'virglrenderer' 'pipewire' 'libva')
source=("https://github.com/containers/libkrun/archive/refs/tags/v$pkgver/$pkgname-$pkgver.tar.gz"
        libkrun-remove-nitro-deps.diff
        libkrun-remove-sev-deps.diff
        libkrun-remove-tdx-deps.diff
        libkrun-relax-bindgen-dep.diff
        libkrun-remove-unused-deps.diff)
sha256sums=('d01240bdd567de81ef8da9776595633f6fade694721622295a033c2c332a544f'
            '3472ad7c6e4084c23bea817b9002bb3d1995910a6f541fcc82ad3c60fadb841c'
            '64325e160648637ee967c1b692995d2cecdbf31e819f547d18c9f1f880d49576'
            'f83b325333683274a9b23416edaceb235e88bad298dd9b8635e3a83ddab42bc6'
            'f6ddd00c1bc8bf6a1562d9d41c8bd9a4485b6be0f404097a1f369f47cbf5af81'
            'ba3e079dda1a65fa06a29bec4b2fc6d65c97bbaa3e979b385531c84c95cdd39f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-remove-unused-deps.diff
  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-remove-nitro-deps.diff
  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-remove-tdx-deps.diff
  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-relax-bindgen-dep.diff
  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-remove-sev-deps.diff
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # TODO: should we set VIRGL_RESOURCE_MAP2=1 ?
  BLK=1 SND=1 GPU=1 NET=1 make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" PREFIX=/usr LIBDIR_Linux=lib install
}
