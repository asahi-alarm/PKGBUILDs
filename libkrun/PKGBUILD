# Maintainer: hexchain <arch at hexchain.org>

pkgname=libkrun
pkgver=1.14.0
pkgrel=1
pkgdesc="A dynamic library providing Virtualization-based process isolation capabilities"
url='https://github.com/containers/libkrun'
arch=('x86_64' 'aarch64')
license=('Apache')
makedepends=('rustup' 'patchelf' 'virglrenderer')
depends=('glibc' 'gcc-libs' 'libkrunfw' 'virglrenderer' 'pipewire' 'libva')
source=("https://github.com/containers/libkrun/archive/refs/tags/v$pkgver/$pkgname-$pkgver.tar.gz"
        libkrun-remove-nitro-deps.diff
        libkrun-remove-sev-deps.diff
        libkrun-remove-unused-deps.diff)
sha256sums=('6e568ce7371f554653eff591506250741e1ffad0fb19abd26202b89d7eefcd17'
            'af9785a1a875d27be4f1712e8d41ec86900b5dfbead5cc51f69ef3810fb16e7f'
            'be05e364c29b94dfce4c130a83543a3e2f2ea6e47f9974bbf8acc25f5005dfba'
            '175bce547292a85a284a0a257b5f0dee5ed7cc1eeeffeb8dd10d8e87395e8c88')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch --no-backup-if-mismatch -f -p2  --fuzz=0 < ../libkrun-remove-unused-deps.diff
  patch --no-backup-if-mismatch -f -p2  --fuzz=0 < ../libkrun-remove-nitro-deps.diff
  patch --no-backup-if-mismatch -f -p2  --fuzz=0 < ../libkrun-remove-sev-deps.diff
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # TODO: should we set VIRGL_RESOURCE_MAP2=1 ?
  BLK=1 SND=1 GPU=1 NET=1 make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" PREFIX=/usr LIBDIR_Linux=lib install
}
