# Maintainer: hexchain <arch at hexchain.org>

pkgname=libkrun
pkgver=1.17.4
pkgrel=1
pkgdesc="A dynamic library providing Virtualization-based process isolation capabilities"
url='https://github.com/containers/libkrun'
arch=('x86_64' 'aarch64')
license=('Apache')
makedepends=('rustup' 'virglrenderer')
depends=('glibc' 'gcc-libs' 'libkrunfw' 'virglrenderer' 'pipewire' 'libva')
source=("https://github.com/containers/libkrun/archive/refs/tags/v$pkgver/$pkgname-$pkgver.tar.gz"
        libkrun-remove-nitro-deps.diff
        libkrun-remove-sev-deps.diff
        libkrun-remove-tdx-deps.diff
        libkrun-bump-bzip-dep.diff
        libkrun-remove-unused-deps.diff)
sha256sums=('2708a3c207c5493ee02de1781836c2511e54eb280633fcc7058fee983a6c2fe3'
            '9ec17a92baa157c890af4ff4446902ffcc4bb513c80785ab404e864d09fb653a'
            '83622e6fa12d821c6c777512935bad95e06f39a0da56e355ef7cf1357841acbf'
            '0b0cd747aababf0ad40a58e8ec7d78e70abc8a28aa02b96ebc70198794aa2b8d'
            '6610eb8bca1ca7123f99d4614b18b615ba3e55ed1c54273c2278480876cbedaa'
            '42164aa8e6562e5ffafcf04a70adab5cd67a333968d0ab9123e6ad3180cce199')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-remove-unused-deps.diff
  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-remove-nitro-deps.diff
  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-remove-tdx-deps.diff
  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-bump-bzip-dep.diff
  patch --no-backup-if-mismatch -f -p1  --fuzz=0 < ../libkrun-remove-sev-deps.diff
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # TODO: should we set VIRGL_RESOURCE_MAP2=1 ?
  BLK=1 SND=1 GPU=1 NET=1 make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" PREFIX=/usr LIBDIR_Linux=lib install
}
