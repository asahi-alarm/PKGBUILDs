# Maintainer: Jos Dehaes <jos.dehaes@gmail.com>
# Maintainer: Matthias Kurz <m.kurz@irregular.at>

pkgname=asahi-scripts
pkgver=20260127.1
pkgrel=1
pkgdesc='Asahi Linux maintenance scripts'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/asahi-alarm/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
install=asahi-scripts.install
sha256sums=('4df3d1e9946fca9b9e37177e9b832d05549096c94e17cf3bd8c192a3b8d1c0bc')
b2sums=('9168a6a5e7e4c7e861e92ae13f8404ae955ab5ed77c6c8811bb31fbe3c88b6997356cf910a93901e5c3122e78102e554314497baf4e3b3ada5e8a731eddd9ebb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} install-arch
  # re-add DTBS variable
  sed -i -e '/${CONFIG/i : ${DTBS:=$(/bin/ls -d /lib/modules/*-ARCH | sort -rV | head -1)/dtbs/*.dtb}' "$pkgdir/usr/bin/update-m1n1"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
