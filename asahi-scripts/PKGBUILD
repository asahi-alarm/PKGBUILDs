# Maintainer: Jos Dehaes <jos.dehaes@gmail.com>
# Maintainer: Matthias Kurz <m.kurz@irregular.at>

pkgname=asahi-scripts
pkgver=20251212.1
pkgrel=1
pkgdesc='Asahi Linux maintenance scripts'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/asahi-alarm/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
install=asahi-scripts.install
sha256sums=('6e064b14ed24c981473de69b969406473852a86765981075ec80c254d1a69ec8')
b2sums=('c213f1aeb376eaadf176598e736a9b998dfb8d72843545b3d2c6af8e5370daa5011b420e7a567868dfaa4be2194ab3309dcf54b3b2dc188e555061fb54cf80fc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} install-arch
  # re-add DTBS variable
  sed -i -e '/${CONFIG/i : ${DTBS:=$(/bin/ls -d /lib/modules/*-ARCH | sort -rV | head -1)/dtbs/*.dtb}' "$pkgdir/usr/bin/update-m1n1"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
