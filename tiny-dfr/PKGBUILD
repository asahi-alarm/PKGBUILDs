# Maintainer: Jos Dehaes <jos.dehaes@gmail.com>
# Maintainer: Matthias Kurz <m.kurz@irregular.at>

pkgname=tiny-dfr
pkgver=0.3.5
pkgrel=2
pkgdesc='Apple silicon touch bar input / display daemon'
arch=('aarch64')
url=https://crates.io/crates/tiny-dfr
license=('MIT' 'APACHE')
makedepends=('rustup')
depends=('pango' 'libinput' 'gdk-pixbuf2' 'librsvg')
source=(
  "$pkgname-$pkgver.crate::https://crates.io/api/v1/crates/tiny-dfr/$pkgver/download"
  'tiny-dfr-fix-metadata.diff')
sha512sums=('0406ad4a8ab3557db4f04128834084d0c872540b4e160a11aec5efe2ca29a7fe512063d50ea713c6d68939ea3d08c0178971c13e0e2bf14face5eafa9f6a891f'
            '90a82d2e25d29cf1501a5222665296128032ee29f04bc6930f60f1234c0a300ae7fdb22962dd6434095f0e0b16c9b07032ab185aff4268ae52a440f2cabee508')

prepare() {
  tar -xf $pkgname-$pkgver.crate
  cd $pkgname-$pkgver
  cat ../tiny-dfr-fix-metadata.diff | patch -p1 -s --fuzz=0 --no-backup-if-mismatch -f
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-$pkgver
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 target/release/$pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 share/$pkgname/* -t "$pkgdir/usr/share/$pkgname"
  install -Dm644 etc/systemd/system/$pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 etc/udev/rules.d/99-touchbar-seat.rules "$pkgdir/usr/lib/udev/rules.d/99-touchbar-seat.rules"
  install -Dm644 etc/udev/rules.d/99-touchbar-tiny-dfr.rules "$pkgdir/usr/lib/udev/rules.d/99-touchbar-tiny-dfr.rules"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 LICENSE.material "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.material
}

# vim:set ts=2 sw=2 et:
